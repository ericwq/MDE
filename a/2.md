```
transformation alma2db(in alma:AlmaMM, out db:DbMM);
 
-- auxiliary property to register whether a column
-- originates from a ‘key’ field
intermediate property Column::inKey : Boolean;
 
-- entry point of the transformation ----------------
 
main() {
   -- we start the transformation by transforming all entities
   -- to tables where we start with the empty prefix for table
   -- and column names
   alma.objectsOfType(Entity)->map entity2table(“);
}
 
-- Mapping operations -----------------------
 
-- Abstract mapping used for records, which simply populates
-- a table with columns by flattening the fields of the record. This
-- rule is invoked implicitly through inheritance declarations
abstract mapping Record::fieldColumns(in prefix : String) : Table {
   init {
      -- Calculate the columns for the leaf fields of this record.
      -- Observe that the predicate self.key = f exploits that if
      -- self does not have the property key, it is null and
      -- thus the predicate would return false
      var mainColumns := self.fields->map(f)
              field2Columns(prefix, self.key = f);
      var recordName := self.name;
   }
}
-- An alma entity maps onto a Table
mapping Entity::entity2table(in prefix : String) : Table
inherits fieldColumns {
   -- inheritance actually means an invocation of
   -- self.initializeTable(result, prefix) where ‘result’ is a Table
   -- instance created before the call (unless the table was passed
   -- to the mapping in the mapping invocation itself)
 
   name := recordName;
 
   -- We build the primary key for the table
   key := object Key {
        -- Note that Column::inKey is temporary data,
        -- which simply reports if the column takes part in a key.
        -- The part [inKey=true] is a filter on columns
        column := result.columns[inKey=true];
      };
   -- We actively construct the tables for top-level dependent parts
   var dependentTables := parts->map
           part2table(prefix + recordName + ‘_’);
 
-- we construct the foreign key columns by fetching the column
-- of each constructed table for the dependent part, which was
-- designated to be a primary key
var foreignKeyColumns := dependentTables->columns[inKey=true];
 
-- the actual columns are the ones originating from fields as
-- well as the foreign key columns
columns := mainColumns + foreignKeyColumns;
 
-- finally, we construct the foreign key objects as well, one
-- for each dependent table
foreignKeys := dependentTables->object(t) ForeignKey
                          {key := t.key;};
}
-- A root dependent part is mapped to a table as well
mapping DependentPart::part2table(in prefix : String) : Table
inherits fieldColumns {
   -- the name of a dependent part table is prefixed to
   -- avoid name confusion
   var dpTableName := prefix + recordName;
   name := dpTableName;
 
   -- we assign the columns for the top-level dependent part
   -- This includes the definition of a primary key
   columns := mainColumns +
        object Column {
           name := ‘key_’ + dpTableName;
           type := ‘INTEGER’;
           -- observe that we set the auxiliary property on true
           inKey
        };
   -- We calculate the columns of the sub parts by passing the
   -- result table of this mapping explicitly to the mapping
   -- part2columns. This means that part2columns will not construct
   -- a new table, but only update the existing one.
   -- We also pass it the new prefix.
end { self.parts->map part2columns(result, dpTableName + ‘_’); }
}
 
-- the following mapping is recursive over the ‘parts’ property
-- of DependentPart
mapping DependentPart::part2columns(in prefix : String) : Table
inherits fieldColumns {
   -- for each nested dependent part, we simply add the newly
   -- resulting columns to those of the result table
   columns += mainColumns;
 
   -- we now recursively call the part2columns again with the
   -- result of this mapping
   var newPrefix := prefix + recordName + ‘_’;
   end { self.parts->map part2columns(result, newPrefix); }
}

-- This mapping flattens a field to multiple columns
mapping Field::field2Columns (in prefix:String, in iskey:Boolean)
: Sequence(Column) {
   init {
      result :=
        if self.type.isTypeOf(PrimitiveType)
        then -- creates a sequence with a unique leaf field instance
        Sequence {
        object Column {
           type := self.type.asType(PrimitiveType)->
                 convertPrimitiveType();
           name := prefix + self.name;
           inKey := iskey;
        }
      }
   else if self.type.isTypeOf(PhysicalQuantityType) then
      self.type.asType(PhysicalQuantityType}->
        pqType2Columns(prefix, iskey);
      }”

   else -- the case for value types is purely recursive
        self.type.asType(ValueType).fields->map
              field2Columns(prefix + self.name + ‘_’, iskey);
        endif
      endif;
   }
}
-- We have one mapping which maps each unit of physical quantity
-- to a column.
mapping PhysicalQuantityType::pqType2Columns (in prefix : String,
              in iskey : Boolean)
: Sequence(Column) {
   init {
      result := self.units->map(u)
           object Column {
              name := prefix + ‘_as_’ + self.name + ‘_in_’ + u;
              type := self->convertPQType();
              inKey := iskey;
           };
   }
}
-- Auxiliary functions for converting primitive types—--
query PrimitiveType::convertPrimitiveType() : String =
   if self.name = “int” then ‘INTEGER’
   else if self.name = “float” then ‘FLOAT’
   else if self.name = “long” then ‘BIGINT’
   else ‘DOUBLE’
   endif endif endif;
 
query PhysicalQuantityType::convertPQType() : String =
   if self.isTypeOf(IntPQType) then ‘INTEGER’
   else if self.isTypeOf(FloatPQType) then ‘FLOAT’
   else if self.isTypeOf(LongPQType) then ‘BIGINT’
   else ‘Double’
endif endif endif;
```
